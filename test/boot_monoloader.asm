	DEVICE ZXSPECTRUM128

        ORG 5D3Bh ; Адрес загрузки программы на бейсике
	
PayloadDestAddress = #C000  ; Куда копировать полезную нагрузку

;-------------------------------------------------------------------------------

Basic:  db 0, 0 ; Номер первой строки бейсика
        dw End - BasicLine ; Размер строки бейсика
BasicLine:
        db 0F9h        ; RANDOMIZE
        db 0C0h        ; USR
        db 030h        ; 0
        db 00Eh        ; Признак закодированного числа, дале 5 байт числа.
        db 000h        ; Должно быть 0
        db 000h        ; Должно быть 0
        db Code & 0FFh ; Младшая часть адреса программы в маш. кодах
        db Code >> 8   ; Старшая часть адреса программы в маш. кодах
        db 000h        ; Должно быть 0
        db 03Ah        ; :
        db 0EAh        ; REM

;-------------------------------------------------------------------------------

Code:   ; Блокируем прерывания
        DI

        ; Копирование полезной нагрузки
        LD HL, Payload
        LD DE, PayloadDestAddress
        LD BC, End - Payload
	LDIR

        ; Запуск
        JP PayloadDestAddress

;-------------------------------------------------------------------------------

        ; Полезная нагрузка
Payload:
        INCBIN "build/boot.bin"

;-------------------------------------------------------------------------------

End:
        SAVEBIN "build/boot.B", Basic, End - Basic
